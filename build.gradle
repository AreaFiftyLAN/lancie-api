import org.springframework.boot.gradle.run.BootRunTask

buildscript {
    ext {
        springBootVersion = '1.5.1.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'

jar {
    baseName = 'lancie-api'
    version = '2017.1.6'
}
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        url "https://github.com/stil4m/maven-repository/raw/master/releases/"
    }
}


configurations {
    // We're using Jetty instead
    compile.exclude module: "spring-boot-starter-tomcat"
    // We're using HikariCP instead
    compile.exclude module: "tomcat-jdbc"
    // We're using Log4j2 instead
    compile.exclude module: "spring-boot-starter-logging"
}

dependencies {
    // Some dependency versions are managed by Spring Boot, refer to
    // https://docs.spring.io/spring-boot/docs/current/reference/html/appendix-dependency-versions.html
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile("org.springframework.boot:spring-boot-starter-security")
    compile("org.springframework.boot:spring-boot-starter-jetty")
    compile("org.springframework.boot:spring-boot-starter-mail")
    compile("org.springframework.boot:spring-boot-starter-thymeleaf")
    compile("org.springframework.boot:spring-boot-devtools")
    compile("org.springframework.boot:spring-boot-starter-log4j2")
    compile("com.fasterxml.jackson.datatype:jackson-datatype-jsr310")
    compile("com.zaxxer:HikariCP")
    compile("org.flywaydb:flyway-core:4.0.3")

    compile("org.projectlombok:lombok:1.16.12")
    compile("com.google.guava:guava:19.0")
    compile("nl.stil4m:mollie-api:2.6.0")

    runtime("org.hsqldb:hsqldb")
    runtime("org.postgresql:postgresql")

    testCompile("org.springframework.boot:spring-boot-starter-test")
    testCompile("io.rest-assured:rest-assured:3.0.2")
}

eclipse {
    classpath {
        containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
        containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.2.1'
}

task bootRunDev(type: BootRunTask) {
    group = 'Application'
    doFirst() {
        main = project.mainClassName
        classpath = sourceSets.main.runtimeClasspath
        systemProperty 'spring.profiles.active', 'dev'
    }
}
