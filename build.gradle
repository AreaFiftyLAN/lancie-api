buildscript {
    dependencies {
        classpath('com.google.cloud.tools:jib-spring-boot-extension-gradle:0.1.0')
    }
}

plugins {
    id 'org.springframework.boot' version '2.4.4'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'idea'
    id 'eclipse'
    id 'jacoco'
    id 'io.freefair.lombok' version '5.3.0'
    id 'com.google.cloud.tools.jib' version '2.8.0'
}

repositories {
    mavenCentral()
    maven {
        url "https://github.com/stil4m/maven-repository/raw/master/releases/"
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

dependencies {
    // Some dependency versions are managed by Spring Boot, refer to
    // https://docs.spring.io/spring-boot/docs/current/reference/html/appendix-dependency-versions.html
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-logging'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-json'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.flywaydb:flyway-core:7.7.2'
    implementation 'com.google.guava:guava:30.1.1-jre'
    implementation 'nl.stil4m:mollie-api:2.7.0'
    implementation 'net.logstash.logback:logstash-logback-encoder:4.11'
    implementation 'in.ashwanthkumar:slack-java-webhook:0.0.9'

    runtimeOnly 'org.hsqldb:hsqldb'
    runtimeOnly 'org.postgresql:postgresql'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    // rest-assured 4.3+ ships with Groovy 3.0, which doesn't play nice with Gradle (yet)
    testImplementation 'io.rest-assured:rest-assured:4.2.1'
    testImplementation 'io.rest-assured:json-path:4.2.1'
    testImplementation 'io.rest-assured:xml-path:4.2.1'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'
}

jib {
    from {
        image = 'wisvch/spring-boot-base:2.1'
    }
    to {
        image = 'quay.io/areafiftylan/lancie-api'
        auth {
            username = System.getenv("QUAY_USERNAME") ?: "" // Set in Github Org Secrets
            password = System.getenv("QUAY_PASSWORD") ?: ""
        }
        tags = [System.getenv("GITHUB_RUN_NUMBER") ?: "latest"]
    }
    pluginExtensions {
        pluginExtension {
            implementation = 'com.google.cloud.tools.jib.gradle.extension.springboot.JibSpringBootExtension'
        }
    }
    container {
        creationTime = 'USE_CURRENT_TIMESTAMP'
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

test {
    useJUnitPlatform()
}

check.dependsOn jacocoTestReport
