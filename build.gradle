import org.springframework.boot.gradle.run.BootRunTask

buildscript {
    ext {
        springBootVersion = '1.5.7.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'jacoco'

jar {
    baseName = 'lancie-api'
    version = '2017.1.6'
    archiveName = "${baseName}.${extension}"
}

springBoot {
    executable = true
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        url "https://github.com/stil4m/maven-repository/raw/master/releases/"
    }
}


configurations {
    // We're using HikariCP instead
    compile.exclude module: "tomcat-jdbc"
    // We're using Log4j2 instead
    compile.exclude module: "spring-boot-starter-logging"
}

dependencies {
    // Some dependency versions are managed by Spring Boot, refer to
    // https://docs.spring.io/spring-boot/docs/current/reference/html/appendix-dependency-versions.html
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile("org.springframework.boot:spring-boot-starter-security")
    compile("org.springframework.boot:spring-boot-starter-mail")
    compile("org.springframework.boot:spring-boot-starter-thymeleaf")
    compile("org.springframework.boot:spring-boot-devtools")
    compile("org.springframework.boot:spring-boot-starter-log4j2")
    compile("org.springframework.boot:spring-boot-starter-actuator")
    compile("com.fasterxml.jackson.datatype:jackson-datatype-jsr310")
    compile("com.zaxxer:HikariCP")
    compile("org.flywaydb:flyway-core:4.2.0")

    compile("org.projectlombok:lombok:1.16.18")
    compile("com.google.guava:guava:23.0")
    compile("nl.stil4m:mollie-api:2.6.2")

    runtime("org.hsqldb:hsqldb")
    runtime("org.postgresql:postgresql")

    testCompile("org.springframework.boot:spring-boot-starter-test")
    testCompile("io.rest-assured:rest-assured:3.0.3")
}

eclipse {
    classpath {
        containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
        containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.1'
}

task bootRunDev(type: BootRunTask) {
    group = 'Application'
    doFirst() {
        main = project.mainClassName
        classpath = sourceSets.main.runtimeClasspath
        systemProperty 'spring.profiles.active', 'dev'
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

check.dependsOn jacocoTestReport